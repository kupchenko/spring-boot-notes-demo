plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

group = 'me.kupchenko'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:2.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    implementation 'mysql:mysql-connector-java'
    implementation 'org.hsqldb:hsqldb'

    implementation 'me.kupchenko:auth-service-api:1.0'

    implementation 'org.mapstruct:mapstruct:1.2.0.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.2.0.Final'

    implementation 'io.github.openfeign:feign-okhttp:10.8'
    implementation("io.github.openfeign:feign-jackson:10.1.0")
    implementation("io.github.openfeign:feign-slf4j:10.1.0")

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    swaggerUI 'org.webjars:swagger-ui:3.22.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

swaggerSources {
    nms {
        inputFile = file('src/main/resources/api.spec.yaml')
    }
}

task swagger(type: Copy) {
    dependsOn generateSwaggerUI
    from "$buildDir/swagger-ui-nms"
    into "$buildDir/resources/main/static/swagger-ui"
    doLast {
        delete "$buildDir/swagger-ui-nms"
    }
}

processResources.dependsOn swagger

test {
    useJUnitPlatform()
}
